*** Settings ***
Documentation     test ezScrumn release plan page    # Speed = 0.05 failed -> 待加入同步機制
Suite Setup       Test Release Plan - PreCondition
Suite Teardown    Test Release Plan - PostCondition
Force Tags        ReleasePlan
Resource          keywords/SprintPlan/ezScrum_SprintPlan_Sprint.txt
Resource          keywords/SprintPlan/ezScrum_SprintPlan_SetupTeardown.txt
Resource          keywords/ReleasePlan/ezScrum_ReleasePlan_SetupTeardown.txt
Resource          keywords/ReleasePlan/ezScrum_ReleasePlan_Management.txt
Resource          keywords/common_resource.txt
Resource          keywords/ezScrum_Login.txt
Resource          keywords/Project/ezScrum_Project.txt
Resource          Global Define.txt
Resource          ServerConfig.txt
Library           BuiltIn
Library           keywords/lib/Selenium2Improved.py
Library           Collections

*** Test Cases ***
Test Release Plan - Add Release
    [Documentation]    \# 待重構 (assert 應該在 Test Case 內而非 keyword)
    [Setup]    Test Release Plan - Add Release Setup
    NewReleasePlan    @{tsReleasePlan}
    # assert
    [Teardown]    Test Release Plan - Add Release Teardown

Test Release Plan - Edit Release
    [Documentation]    \# 待重構 (assert 應該在 Test Case 內而非 keyword)    # Speed = 0.05 failed -> 待加入同步機制
    [Setup]    Test Release Plan - Edit Release Setup
    EditReleasePlan    @{tsReleasePlan}
    # assert
    [Teardown]    Test Release Plan - Edit Release Teardown

Test Release Plan - Show Release Backlog
    [Setup]    Test Release Plan - Show Release Backlog Setup
    ShowReleaseBacklog    @{tsReleasePlan}
    # assert ?
    [Teardown]    Test Release Plan - Show Release Backlog Teardown

Test Release Plan - Delete Release
    [Setup]    Test Release Plan - Delete Release Setup
    DeleteReleasePlan    @{tsReleasePlan}
    # assert
    ${_itemCount} =    Get Matching Xpath Count    //table[@class='x-treegrid-root-table']/tbody
    Should Be Equal    ${_itemCount}    0
    [Teardown]    Test Release Plan - Delete Release Teardown

Test Release Plan - Contain Sprints
    [Documentation]    建立四個sprint後, 建立一個包含這些sprint的release, 檢查是否有被納入
    [Setup]    Test Release Plan - Contain Sprints Setup
    Go To Page    Sprint Plan
    ${sprintStartDate}=    Get Start Date    NOW
    Create Sprint with Date    sprint1 sprint goal    ${sprintStartDate}    2    4    100    100
    ${sprintStartDate}=    Get Start Date    NOW +14d
    Create Sprint with Date    sprint2 sprint goal    ${sprintStartDate}    2    4    100    100
    ${sprintStartDate}=    Get Start Date    NOW +28d
    Create Sprint with Date    sprint3 sprint goal    ${sprintStartDate}    2    4    100    100
    ${sprintStartDate}=    Get Start Date    NOW +42d
    Create Sprint with Date    sprint4 sprint goal    ${sprintStartDate}    2    4    100    100
    Go To Page    Release Plan
    ${startDateFormat}=    Get Start Date    NOW
    ${endDateFormat}=    Get Start Date    NOW +56d
    Set List Value    ${tsReleasePlan}    1    ${startDateFormat}
    Set List Value    ${tsReleasePlan}    2    ${endDateFormat}
    NewReleasePlan    @{tsReleasePlan}
    Xpath Should Match X Times    //table[@class='x-treegrid-root-table']/tbody//div[text()="testReleasePlan"]/../../..//div[text()="sprint1 sprint goal"]    1
    Xpath Should Match X Times    //table[@class='x-treegrid-root-table']/tbody//div[text()="testReleasePlan"]/../../..//div[text()="sprint2 sprint goal"]    1
    Xpath Should Match X Times    //table[@class='x-treegrid-root-table']/tbody//div[text()="testReleasePlan"]/../../..//div[text()="sprint3 sprint goal"]    1
    Xpath Should Match X Times    //table[@class='x-treegrid-root-table']/tbody//div[text()="testReleasePlan"]/../../..//div[text()="sprint4 sprint goal"]    1
    [Teardown]    Test Release Plan - Contain Sprints Teardown

Test Release Plan - New Sprint
    [Documentation]    建立release後直接在該頁面建立sprint,檢查是否有被納入
    [Setup]    Test Release Plan - New Sprint Setup
    Create Sprint In Release Plan    sprint1 sprint goal    2016/07/04    2    4    100    100
    Wait Until Page Contains    sprint1 sprint goal
    Xpath Should Match X Times    //div[@id="ReleasePlan_ReleaseTree"]//div[text()="sprint1 sprint goal"]    1
    [Teardown]    Test Release Plan - New Sprint Teardown

Test Release Plan - Edit Sprint
    [Setup]    Test Release Plan - Edit Sprint Setup
    Mouse Down    xpath=//div[@id="ReleasePlan_ReleaseTree"]//div[text()="sprint1 sprint goal"]
    Mouse Up    xpath=//div[@id="ReleasePlan_ReleaseTree"]//div[text()="sprint1 sprint goal"]
    Click Element    xpath=//div[@id="ReleasePlan_ReleaseTree"]//div[text()="sprint1 sprint goal"]
    Edit Sprint In Release Plan    sprint1 sprint goal updated    2    5    200    200    sprint1 sprint goal
    Xpath Should Match X Times    //div[@id="ReleasePlan_ReleaseTree"]//div[text()="sprint1 sprint goal updated"]    1
    [Teardown]    Test Release Plan - Edit Sprint Teardown

Test Release Plan - Show Printable Release
    [Setup]    Test Release Plan - Show Printable Release Setup
    Go To Page    Release Plan
    Click Element    xpath=//div[text()="Release One"]
    Click Element    xpath=//button[text()="Show Printable Release"]
    Select Window    Release Plan #1
    #Release One
    Assert Local Text Is Same    //td[1]/table[1]/tbody/tr[1]/td[1]    Release Plan #1：Release One
    Assert Local Text Is Same    //td[1]/table[1]/tbody/tr[2]/td[1]    Start Date：
    Assert Local Text Is Same    //td[1]/table[1]/tbody/tr[2]/td[2]    2016/10/24
    Assert Local Text Is Same    //td[1]/table[1]/tbody/tr[3]/td[1]    End Date：
    Assert Local Text Is Same    //td[1]/table[1]/tbody/tr[3]/td[2]    2016/11/13
    Assert Local Text Is Same    //td[1]/table[1]/tbody/tr[4]/td[1]    Description：
    Assert Local Text Is Same    //td[1]/table[1]/tbody/tr[4]/td[2]    Description for Release One${SPACE}
    #Sprint One
    Assert Local Text Is Same    //table[2]/tbody/tr[2]/td[2]    Sprint #1: Sprint One${SPACE}
    Assert Local Text Is Same    //table[2]/tbody/tr[3]/td[2]    Start Date: 2016/10/24${SPACE}
    Assert Local Text Is Same    //table[2]/tbody/tr[4]/td[2]    Demo Date: 2016/10/28${SPACE}
    Assert Local Text Is Same    //table[2]/tbody/tr[5]/td[2]    ${SPACE}End Date: 2016/10/30${SPACE}
    Assert Local Text Is Same    //table[2]/tbody/tr[5]/td[3]    Total Story Points: 33
    #Story Two
    Printable Release Check Story Bar    6
    Printable Release Check Story Value    7    2${SPACE}    Story Two${SPACE}    82${SPACE}    22${SPACE}
    #Story One
    Printable Release Check Story Value    8    1${SPACE}    Story One${SPACE}    81${SPACE}    11${SPACE}
    #Sprint Two
    Assert Local Text Is Same    //table[2]/tbody/tr[10]/td[2]    Sprint #2: Sprint Two${SPACE}
    Assert Local Text Is Same    //table[2]/tbody/tr[11]/td[2]    Start Date: 2016/10/31${SPACE}
    Assert Local Text Is Same    //table[2]/tbody/tr[12]/td[2]    Demo Date: 2016/11/11${SPACE}
    Assert Local Text Is Same    //table[2]/tbody/tr[13]/td[2]    ${SPACE}End Date: 2016/11/13${SPACE}
    Assert Local Text Is Same    //table[2]/tbody/tr[13]/td[3]    Total Story Points: 33
    #Story Three
    Printable Release Check Story Bar    14
    Printable Release Check Story Value    15    3${SPACE}    Story Three${SPACE}    83${SPACE}    33${SPACE}
    [Teardown]    Test Release Plan - Show Printable Release Teardown
