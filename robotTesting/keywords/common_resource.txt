*** Settings ***
Library           BuiltIn
Library           HttpLibrary.HTTP
Library           lib/DatabaseHandler.py
Library           lib/Selenium2Improved.py

*** Keywords ***
ezScrum Suite Setup
    #    Wait Until Keyword Succeeds    1min    5s    _SeleniumServerCheck    200
    #    Wait Until Keyword Succeeds    1min    5s    _DeployEZScrumServerCheck    200

ezScrum Suite Teardown

_SeleniumServerCheck
    [Arguments]    ${status}
    Get    http://127.0.0.1:4444/wd/hub
    Log Response Status
    Follow Response
    Response Status Code Should Equal    ${status}

_DeployEZScrumServerCheck
    [Arguments]    ${status}
    Get    ${LOGIN_URL}/logon.do
    Log Response Status
    Response Status Code Should Equal    ${status}

New Tab With Alias
    [Arguments]    ${loginUrl}    ${alias}
    [Documentation]    1. 透過 selenium api 的 openWindow 來完成開分頁的功能
    ...    2. 使用此keyword的限制 : open browser的url domain name必須與欲新增的分頁domain name一致
    Open Tab    ${loginUrl}    ${alias}
    Select Window    ezScrum, SSLab NTUT

Compare Field Content
    [Arguments]    ${xpath}    ${correctValue}
    ${testValue}=    Get Value    xpath=${xpath}
    Should Be Equal    ${testValue}    ${correctValue}

Select Project
    [Arguments]    ${PROJECT_NAME}
    Comment    Wait Until Page Contains Element    //div[@id="Projects_GirdPanel"]//table/tbody/tr/td[1]/div[text()="${PROJECT_NAME}"]/../../../../..
    Wait Until Element Is Visible    //div[@id="Projects_GirdPanel"]//table/tbody/tr/td[1]/div[text()="${PROJECT_NAME}"]/../../../../..    15
    Click Element    //div[@id="Projects_GirdPanel"]//table/tbody/tr/td[1]/div[text()="${PROJECT_NAME}"]/../../../../..

Select Node Product
    [Arguments]    ${nodeName}
    Wait Until Element Is Visible    xpath=//span[text()="${nodeName}"]    15
    Click Element    xpath=//span[text()="${nodeName}"]

Element Enable And Submit
    [Arguments]    ${xpathElement}
    Element Should Be Visible    xpath=${xpathElement}
    Click Element    xpath=${xpathElement}

Focus AND Input Text
    [Arguments]    ${xpathElement}    ${text}
    Focus    xpath=${xpathElement}
    Input Text    xpath=${xpathElement}    ${text}

Find Current Window Element
    [Arguments]    ${currentWindowTitle}    ${elementName}
    ${currentWindowElement}=    Set Variable    //span[text()="${currentWindowTitle}"]/../../../../../div[@class="x-window-bwrap"]/.//tbody[ @class="x-btn-small x-btn-icon-small-left" and .//button[text()="${elementName}"] ]
    Xpath Should Match X Times    ${currentWindowElement}    1
    [Return]    ${currentWindowElement}

Enter ezScrum System
    Open Browser    ${LOGIN_URL}    ${BROWSER}
    Input Text    userId    ${gLOGIN_INFO[1]}
    Input Password    password    ${gLOGIN_INFO[2]}
    Submit Form    logonForm
    Title Should Be    ezScrum, SSLab NTUT

Exit ezScrum System
    Click Image    images/logout.gif
    Wait Until Page Contains    ezScrum Logon!
    Title Should Be    ezScrum Logon!
    Close Browser
    Close All Browsers

Logon ezScrum System
    Click Image    images/logout.gif
    Title Should Be    ezScrum Logon!
    Wait Until Page Contains Element    userId
    Wait Until Page Contains Element    password

Logout ezScrum System
    Click Image    images/logout.gif
    Title Should Be    ezScrum Logon!

SwitchToPage
    [Arguments]    ${pageName}
    [Documentation]    Page Name:
    ...
    ...    ProjectList
    ...    Management
    Sleep    5s
    Wait Until Element Is Visible    xpath=//tbody/tr/td[text()="${pageName}"]
    Click Element    xpath=//tbody/tr/td[text()="${pageName}"]

SwitchToItemLink
    [Arguments]    ${isProjectPage}    ${itemName}
    [Documentation]    切換頁面的某個功能選項
    ...    預設是ProjectList頁面
    ${_tagName}=    Set Variable If    ${isProjectPage} == 'True'    span    u
    ${_tagName}=    Set Variable If    ${isProjectPage} == 'False'    u    span
    Click Element    xpath=//li//${_tagName}[.="${itemName}"]
    # 加入同步機制比較妥當

DropDownMenu_Select
    [Arguments]    ${menuName}    ${itemName}
    # focus on Menu
    Click Element    xpath=//input[@name="${menuName}"]
    # select Item
    Click Element    xpath=//div[@class="x-combo-list-inner"]/div[text() = "${itemName}"]

TableGrid3_SelectItem
    [Arguments]    ${columnIndex}    ${itemName}    # index start from 0
    [Documentation]    點選 Table Grid 的第 n 個欄位
    ...    (必須要給此欄位所顯示的名稱才能定位)
    # //div[@class= "x-grid3-cell-inner x-grid3-col-0" and text()="lab1321a"]
    Mouse Down    xpath=//div[@class="x-grid3-cell-inner x-grid3-col-${columnIndex}" and .="${itemName}"]
    Mouse Up    xpath=//div[@class="x-grid3-cell-inner x-grid3-col-${columnIndex}" and text()="${itemName}"]
    Click Element    xpath=//div[@class="x-grid3-cell-inner x-grid3-col-${columnIndex}" and .="${itemName}"]

TableGrid3_GetItemCount
    [Arguments]    ${TableName}
    [Documentation]    給定 Table 名稱,可回傳總資料項目的筆數
    # get item count
    ${ItemCount} =    Get Matching Xpath Count    //span[.="${TableName}"]/../..//div[@class="x-grid3-body"]//tr
    Log    ItemCount = ${ItemCount}
    [Return]    ${ItemCount}

Clean DB
    [Arguments]    ${DB_URL}    ${DB_ACCOUNT}    ${DB_PASSWORD}    ${DB_NAME}
    [Documentation]    GAE 清除資料庫的方式稍後再補上
    Run Keyword if    "${VERSION}"=="Web"    clean database    ${DB_URL}    ${DB_ACCOUNT}    ${DB_PASSWORD}    ${DB_NAME}

Compare Text In Specific Xpath
    [Arguments]    ${xpathToAssert}    ${expectedText}
    ${textOfXpath}=    Get Text    xpath=${xpathToAssert}
    Should Be Equal    ${textOfXpath}    ${expectedText}

Clean Database and Exit ezScrum
    Data Base Tear Down
    Exit ezScrum System

Data Base Tear Down
    Clean DB    ${DB_URL}    ${DB_ACCOUNT}    ${DB_PASSWORD}    ${DB_NAME}

Select Sprint Plan
    Wait Until Page Contains    Sprint Plan
    Click Element    xpath=//span[text()="Sprint Plan"]

Create Sprint With Typing Zero In Interval
    [Arguments]    ${sprintGoal}    ${sprintMembers}    ${sprintAvaliableDays}    ${sprintFocusFactor}
    #Create New Sprint
    Click Element    xpath=//table[@id="SprintPlan_addSprintBtn"]//button[text()="New Sprint"]
    Focus AND Input Text    //textarea[@name="Goal"]    ${sprintGoal}
    Click Image    xpath=//input[@name="StartDate"]/../img
    Click Element    xpath=//button[text()="Today"]
    Focus AND Input Text    //input[@name="Interval"]    0
    Focus AND Input Text    //input[@name="Members"]    ${sprintMembers}
    Focus AND Input Text    //input[@name="AvaliableDays"]    ${sprintAvaliableDays}
    Focus AND Input Text    //input[@name="FocusFactor"]    ${sprintFocusFactor}
    ${sprintDemoDate}=    Get Value    xpath=//input[@name="StartDate"]
    Focus AND Input Text    //input[@name="DemoDate"]    ${sprintDemoDate}

Assert Local Text Is Same
    [Arguments]    ${Local}    ${string}
    ${text}=    Get Text    ${Local}
    Should Be Equal As Strings    ${text}    ${string}

Element Class Should Be Disabled
    [Arguments]    ${ElementLocator}
    [Documentation]    Assert if an element is Disabled without using "Element Should Be Disabled" keyword.
    ...    The 'class' attribute of given element should contain "x-item-disabled".
    ${ElementClass}=    Get Element Attribute    ${ElementLocator}@class
    Should Contain    ${ElementClass}    x-item-disabled

Element Class Should Be Enabled
    [Arguments]    ${ElementLocator}
    ${ElementClass}=    Get Element Attribute    ${ElementLocator}@class
    Should Not Contain    ${ElementClass}    x-item-disabled
